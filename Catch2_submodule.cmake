cmake_minimum_required(VERSION 3.16)

include_guard()

if(TARGET Catch2::Catch2)
	include(Catch)
	enable_testing()
	return()
endif()

if(EXISTS ${CMAKE_CURRENT_LIST_DIR}/Catch2/CMakeLists.txt)
	message(STATUS "Using Catch2 from ${CMAKE_CURRENT_LIST_DIR}/Catch2 as a subdirectory")
	add_subdirectory(${CMAKE_CURRENT_LIST_DIR}/Catch2)
	list(APPEND CMAKE_MODULE_PATH "${CMAKE_CURRENT_LIST_DIR}/Catch2/contrib")
elseif(EXISTS ${CMAKE_CURRENT_BINARY_DIR}/Catch2)
	set(Catch2_root "${CMAKE_CURRENT_BINARY_DIR}/Catch2")	
	message(STATUS "Using Catch2 from ${CMAKE_CURRENT_BINARY_DIR}/Catch2 as a subdirectory")
	add_subdirectory(${CMAKE_CURRENT_BINARY_DIR}/Catch2 ${CMAKE_CURRENT_BINARY_DIR}/Catch2)
	list(APPEND CMAKE_MODULE_PATH "${CMAKE_CURRENT_BINARY_DIR}/Catch2/contrib")
else()
	find_package(Catch2 QUIET)
	if(NOT TARGET Catch2::Catch2)
		message(STATUS "Catch2 not found, downloading and extracting it to ${CMAKE_CURRENT_BINARY_DIR}/Catch2 ...")
		file(DOWNLOAD https://codeload.github.com/catchorg/Catch2/zip/3816e99d0c74882eeaaf30ebb48a07a9418f295c ${CMAKE_CURRENT_BINARY_DIR}/Catch2.zip SHOW_PROGRESS)
		file(SHA256 ${CMAKE_CURRENT_BINARY_DIR}/Catch2.zip Catch2_hash)
		if(NOT Catch2_hash STREQUAL 7665ba7a6dfc6fad52d07311b9face26dfd7affe21bfe1b1d056f1cb2f2cdaec)
			message(FATAL_ERROR "Hash mismatch on Catch2 src zip file.")
		endif()
		execute_process(
			COMMAND ${CMAKE_COMMAND} -E tar xzf "${CMAKE_CURRENT_BINARY_DIR}/Catch2.zip"
			WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}
		)
		file(GLOB Catch2_src_dir LIST_DIRECTORIES true RELATIVE ${CMAKE_CURRENT_BINARY_DIR} "${CMAKE_CURRENT_BINARY_DIR}/Catch2-*/")
		file(RENAME "${CMAKE_CURRENT_BINARY_DIR}/${Catch2_src_dir}" "${CMAKE_CURRENT_BINARY_DIR}/Catch2")
		set(Catch2_root "${CMAKE_CURRENT_BINARY_DIR}/Catch2")
		message(STATUS "Using Catch2 from ${CMAKE_CURRENT_BINARY_DIR}/Catch2 as a subdirectory")
		add_subdirectory(${CMAKE_CURRENT_BINARY_DIR}/Catch2 ${CMAKE_CURRENT_BINARY_DIR}/Catch2)
		list(APPEND CMAKE_MODULE_PATH "${CMAKE_CURRENT_BINARY_DIR}/Catch2/contrib")
	else()
		message(STATUS "Using Catch2 from the found package")
	endif()
endif()

include(Catch)
enable_testing()
